
plugins {
    id "org.sonarqube" version "2.6.1"
    id "org.xbib.gradle.plugin.asciidoctor" version "1.5.4.1.0"
    id "io.codearte.nexus-staging" version "0.11.0"
}

printf "Host: %s\nOS: %s %s %s\nJVM: %s %s %s %s\nGroovy: %s\nGradle: %s\n" +
        "Build: group: ${project.group} name: ${project.name} version: ${project.version}\n",
        InetAddress.getLocalHost(),
        System.getProperty("os.name"),
        System.getProperty("os.arch"),
        System.getProperty("os.version"),
        System.getProperty("java.version"),
        System.getProperty("java.vm.version"),
        System.getProperty("java.vm.vendor"),
        System.getProperty("java.vm.name"),
        GroovySystem.getVersion(),
        gradle.gradleVersion

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: "jacoco"
    apply plugin: 'org.xbib.gradle.plugin.asciidoctor'

    repositories {
        mavenCentral()
    }

    configurations {
        asciidoclet
        wagon
    }

    dependencies {
        testCompile "junit:junit:${project.property('junit.version')}"
        testCompile "com.fasterxml.jackson.core:jackson-databind:${project.property('jackson.version')}"
        asciidoclet "org.asciidoctor:asciidoclet:${project.property('asciidoclet.version')}"
        wagon "org.apache.maven.wagon:wagon-ssh:${project.property('wagon.version')}"
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true
        showViolations = true
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all,-serial" << "-profile" << "compact1"
    }

    jar {
        manifest {
            attributes('Implementation-Version': project.version)
        }
    }

    clean {
        delete 'out'
    }

    test {
        testLogging {
            showStandardStreams = false
            exceptionFormat = 'full'
        }
        systemProperty 'java.net.preferIPv4Stack', 'false'
        systemProperty 'java.net.preferIPv6Addresses', 'true'
    }

    asciidoctor {
        attributes 'source-highlighter': 'coderay',
                toc: 'left',
                doctype: 'book',
                icons: 'font',
                encoding: 'utf-8',
                sectlink: true,
                sectanchors: true,
                linkattrs: true,
                imagesdir: 'img'
    }

    javadoc {
        options.docletpath = configurations.asciidoclet.files.asType(List)
        options.doclet = 'org.asciidoctor.Asciidoclet'
        options.overview = "src/docs/asciidoclet/overview.adoc"
        options.addStringOption "-base-dir", "${projectDir}"
        options.addStringOption "-attribute",
                "name=${project.name},version=${project.version},title-link=https://github.com/xbib/${project.name}"
        configure(options) {
            noTimestamp = true
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    if (project.hasProperty('signing.keyId')) {
        signing {
            sign configurations.archives
        }
    }

    apply from: "${rootProject.projectDir}/gradle/ext.gradle"
    apply from: "${rootProject.projectDir}/gradle/publish.gradle"
    apply from: "${rootProject.projectDir}/gradle/sonarqube.gradle"
}

nexusStaging {
    packageGroup = "org.xbib"
}
